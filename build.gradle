buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.netflix.nebula:gradle-ospackage-plugin:2.2.6'
	}
}

apply plugin: 'os-package'
apply plugin: 'rpm'

buildDeb.enabled = false
buildRpm.enabled = false

ospackage {
	version = '1.3'
	release = 1
	arch = NOARCH
	os = LINUX
	user 'root'
	permissionGroup = 'root'
	packageDescription = 'Spring XD plugin contains Ambari\'s stack definition for Spring XD service.'
	packageGroup = 'Applications/Databases'
	license = 'Apache License v2.0'

	requires('ambari-server', '1.7', GREATER | EQUAL)
	requires('python', '2.6', GREATER | EQUAL)

	postInstall file('src/main/package/rpm/postinstall.sh')
	preUninstall file('src/main/package/rpm/preremove.sh')

}

task phd30Rpm(type: Rpm) {
	packageName = 'spring-xd-plugin-phd'
	from('src/main/resources/services') {
		into '/var/lib/ambari-server/resources/stacks/PHD/3.0/services'
	}
}

task hdp22Rpm(type: Rpm) {
	packageName = 'spring-xd-plugin-hdp'
	from('src/main/resources/services') {
		into '/var/lib/ambari-server/resources/stacks/HDP/2.2/services'
	}
}

task dist {
	dependsOn phd30Rpm, hdp22Rpm
}

